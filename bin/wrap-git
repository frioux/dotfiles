#!/usr/bin/perl

use strict;
use warnings;

$0 = 'git';

my %commit = (
   commit => 1,
   ci     => 1,
),

my %checkout = (
   checkout => 1,
   co       => 1,
);

exec 'git' unless @ARGV;

my ($command, @args) = @ARGV;

if ($commit{$command} && exists $args[0] && $args[0] =~ /^-a|^--all/) {
   my @staged = grep /^M/, split /\0/, `git status -z`;
   if (@staged) {
      my @diff = `git diff --shortstat`;
      if (@diff) {
         print "There are staged changes, are you sure you want to commit all? (y/N) ";
         chomp(my $answer = <STDIN>);
         unless ($answer =~ /^y/i) {
            exit 2;
         }
      }
   }
} elsif ($checkout{$command} && ( grep /^-b|^-B/, @args ) && ( grep /^origin\/(?:release|master)/, @args )) {
   unshift @args, '--no-track';
} elsif ($command eq 'pull' && !grep /^-/, @args) {
   unshift @args, '--ff-only';
} elsif ($command eq 'clean' && grep /^-xdff$/, @args) {
   die "Frew your muscle memory is some kind of murder (add another f if you really want it)\n"
} elsif ($command eq 'clean' && grep /^-xdfff$/, @args) {
   s/-xdfff/-xdff/ for @args
}

exec 'git', $command, @args
