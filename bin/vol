#!/usr/bin/env perl

use strict;
use warnings;

use List::Util 'max', 'min';

use JSON::PP;

my $s = qx(pacmd list-sinks);
my @sinks = split /\n\s+(?:\*\s+)?index:\s+/, $s ;

shift @sinks;

my ($sink_id, $sink_data) = pick_sink(@sinks);
my ($vol, $muted) = parse_sink($sink_data);

my $cmd = shift || "help";

if (my $sub = main->can("cmd_$cmd")) {
   $sub->(@ARGV);
} else {
   die "no such command $cmd\n"
}

sub _notify {
   my $text = shift;
   system 'killall -TERM dzen2';
   system "echo '$text' | dzen2 -p 1 &"
}

sub pick_sink {
   my @sinks = @_;

   for my $sink (@sinks) {
      if ($sink =~ m/ibasso/i) {
         my ($ret) = ($sink =~ m/^(\d+)/);

         return ( $ret, $sink );
      }
   }

   for my $sink (@sinks) {
      if ($sink =~ m/hda intel pch/i) {
         my ($ret) = ($sink =~ m/^(\d+)/);

         return ( $ret, $sink );
      }
   }

   my ($ret) = ($sinks[0] =~ m/^(\d+)/);

   return ( $ret, $sinks[0] );
}

sub parse_sink {
   my $sink_data = shift;

   my ( $vol ) = ($sink_data =~ m/volume: f.*?(\d+)%/);
   my $muted = $sink_data =~ m/muted: yes/;
   $muted ||= 0; # prettier output in status

   return ( $vol, $muted );
}

sub cmd_status {
   print "sink_id: $sink_id\nvolume: $vol\nmuted: $muted\n";
}

sub cmd_toggle {
   _notify($muted ? 'Unmuted!' : 'Muted :(');
   exec 'pacmd', 'set-sink-mute', $sink_id, ( $muted ? 0 : 1 );
}

sub cmd_up {
   my $new_vol = min(int 65536 * ($vol + 2) / 100, 65536);
   _notify('Increased volume to ' .  sprintf "%d%%", 100 * ($new_vol / 65536));
   exec 'pacmd', 'set-sink-volume', $sink_id, $new_vol
}

sub cmd_down {
   my $new_vol = min(int 65536 * ($vol - 2) / 100, 65536);
   _notify('Decreased volume to ' .  sprintf "%d%%", 100 * ($new_vol / 65536));
   exec 'pacmd', 'set-sink-volume', $sink_id, $new_vol
}

sub cmd_help {
   print <<HELP
vol [help]             - this message
     up   <percentage> - increase volume by percentage
     down <percentage> - decrease volume by percentage
     status            - prints selected sink, mute status, and volume
HELP
}
