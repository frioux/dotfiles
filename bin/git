#!/usr/bin/perl

use strict;
use warnings;

$0 = 'git';

my %commit = (
   commit => 1,
   ci     => 1,
),

my %checkout = (
   checkout => 1,
   co       => 1,
);

# git --git-dir=/home/frew/code/dotfiles/.git --no-pager grep -n --no-color foo

exec '/usr/bin/git' unless @ARGV;

my ($precmd, $command, $args) = _parse_args(@ARGV);
my @args = @$args;

if ($commit{$command} && exists $args[0] && $args[0] =~ /^-a|^--all/) {
   my @staged = grep /^M/, split /\0/, `git status -z`;
   if (@staged) {
      my @diff = `git diff --shortstat`;
      if (@diff) {
         print "There are staged changes, are you sure you want to commit all? (y/N) ";
         chomp(my $answer = <STDIN>);
         unless ($answer =~ /^y/i) {
            exit 2;
         }
      }
   }
} elsif ($checkout{$command} && ( grep /^-b|^-B/, @args ) && ( grep /^origin\/(?:release|master)/, @args )) {
   unshift @args, '--no-track';
} elsif ($command eq 'clean' && grep /^-xdff$/, @args) {
   die "Frew your muscle memory is some kind of murder (add another f if you really want it)\n"
} elsif ($command eq 'clean' && grep /^-xdfff$/, @args) {
   s/-xdfff/-xdff/ for @args
} elsif ($command eq 'remote' && $args[0] eq 'add' && @args == 2) {
   @args = ('add', 'origin', $args[-1]);
} elsif ($command eq 'grep' && -f '.git/info/grep-exclude') {

   my @exclude;

   open my $fh, '<', '.git/info/grep-exclude';
   while (<$fh>) {
      chomp;
      next if m/^#/;
      push @exclude, ":!$_";
   }
   close $fh;
   if (1 == grep !m/^-/, @args) {
      push @args, '--', ':./*', @exclude;
   } else {
      # left is stuff that starts with -
      # right is stuff that doesn't (sorta)
      my $left = 1;
      my (@left, @right);
      for my $val (@args) {
         if ($left && $val =~ m/^-/) {
            push @left, $val;
         } else {
            $left = 0;
            push @right, $val;
         }
      }
      @args = (@left, $right[0], '--', @right[1..$#right], @exclude)
   }
}

my @run = ('/usr/bin/git', @$precmd, $command, @args);
warn join(' ', @run) . "\n" if $ENV{GIT_WRAPPER_DEBUG};
exec @run;

sub _parse_args {
   my @argv = @_;

   my (@precmd, $cmd, @postcmd);

   for my $arg (@argv) {
      if (!$cmd) {
         if ($arg =~ m/^-/) {
            push @precmd, $arg
         } else {
            $cmd = $arg
         }
      } else {
         push @postcmd, $arg
      }
   }

   return \@precmd, $cmd, \@postcmd;
}
