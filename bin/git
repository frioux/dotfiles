#!/usr/bin/perl

use strict;
use warnings;

use Dir::Self;

$0 = 'git';

my $dir = __DIR__;

local $ENV{PATH} = join ':',
   grep { $_ ne $dir }
   split qr/\:/,
   $ENV{PATH};

my %commit_aliases = (
   commit => 1,
   ci     => 1,
),

my %checkout_aliases = (
   checkout => 1,
   co       => 1,
);

my %sendemail_aliases = ( 'send-email' => 1 );

exec 'git' unless @ARGV;

my ($command, @args) = @ARGV;

if ($commit_aliases{$command} && exists $args[0] && $args[0] =~ /^-a|^--all/) {
   my @staged = grep /^M/, split /\0/, `git status -z`;
   if (@staged) {
      print "There are staged changes, are you sure you want to commit all? (y/N) ";
      chomp(my $answer = <STDIN>);
      if ($answer =~ /^y/i) {
         run_command()
      }
   } else {
      run_command()
   }
} elsif ($checkout_aliases{$command} && ( grep /^-b|^-B/, @args ) && ( grep /^origin\/(release|master)/, @args )) {
   unshift @args, '--no-track';
   run_command()
} elsif ($sendemail_aliases{$command}) {
   chomp (my $pass = qx(netrc-password smpt.gmail.com));
   unshift @args, '--smtp-pass', $pass;
   run_command()
} elsif ($command eq 'pull' && !grep /^-/, @args) {
   unshift @args, '--ff-only';
   run_command()
} else {
   run_command()
}

sub run_command { exec 'git', $command, @args }
