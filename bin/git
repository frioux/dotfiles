#!/usr/bin/perl

use strict;
use warnings;

use Dir::Self;

$0 = 'git';

my $dir = __DIR__;

local $ENV{PATH} = join ':',
   grep { $_ ne $dir }
   split qr/\:/,
   $ENV{PATH};

my %commit = (
   commit => 1,
   ci     => 1,
),

my %checkout = (
   checkout => 1,
   co       => 1,
);

my %sendemail = ( 'send-email' => 1 );

exec 'git' unless @ARGV;

my ($command, @args) = @ARGV;

if ($commit{$command} && exists $args[0] && $args[0] =~ /^-a|^--all/) {
   my @staged = grep /^M/, split /\0/, `git status -z`;
   if (@staged) {
      my @diff = `git diff --shortstat`;
      if (@diff) {
         print "There are staged changes, are you sure you want to commit all? (y/N) ";
         chomp(my $answer = <STDIN>);
         unless ($answer =~ /^y/i) {
            exit 2;
         }
      }
   }
} elsif ($checkout{$command} && ( grep /^-b|^-B/, @args ) && ( grep /^origin\/(?:release|master)/, @args )) {
   unshift @args, '--no-track';
} elsif ($sendemail{$command}) {
   chomp (my $pass = qx(netrc-password smtp.gmail.com));
   unshift @args, '--smtp-pass', $pass;
} elsif ($command eq 'pull' && !grep /^-/, @args) {
   unshift @args, '--ff-only';
}

exec 'git', $command, @args
