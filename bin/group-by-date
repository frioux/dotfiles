#!/usr/bin/env perl

use strict;
use warnings;

use Text::xSV;
use DateTime;
use DateTime::Format::Strptime;
use Getopt::Long::Descriptive;

my ($opt, $usage) = describe_options(
  'group-by-date %o',
  [ 'in_format|i=s', 'format to parse from',  { default => '%F %T.%N' } ],
  [ 'by_day|d', 'set in_format to "%F %T" (YYYY-MM-DD HH:MM:SS)'],
  [ 'group_format|g=s', 'format to group by', { default => '%F'       } ],
  [ 'out_format|o=s', 'format to output',   { default => '%F %T.%N' } ],
  [],
  [ 'help|h', 'view help' ],
);

print($usage->text), exit if $opt->help;

my $f_in = new DateTime::Format::Strptime(
   pattern     => ($opt->by_day ? '%F %T' : $opt->in_format),
   locale      => 'en_US',
   time_zone   => 'UTC',
);

my $f_out = new DateTime::Format::Strptime(
   pattern     => $opt->out_format || $opt->group_format,
   locale      => 'en_US',
   time_zone   => 'UTC',
);

my $f_group = new DateTime::Format::Strptime(
   pattern     => $opt->group_format,
   locale      => 'en_US',
   time_zone   => 'UTC',
);

my $csv = Text::xSV->new( fh => *STDIN );

my %date;
while (my $row = $csv->get_row) {
   my ($time, @val) = @$row;
   my $date_string = $f_group->format_datetime($f_in->parse_datetime($time));
   $date{$date_string} ||= [];
   @val = (1) unless @val;
   $date{$date_string}[$_] += $val[$_] for 0..$#val;
}

my $csv_out = Text::xSV->new( fh => *STDOUT );

$csv_out->print_row(
   $f_out->format_datetime($f_group->parse_datetime($_)),
   @{$date{$_}}
) for sort keys %date;
